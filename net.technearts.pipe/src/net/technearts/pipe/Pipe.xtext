grammar net.technearts.pipe.Pipe hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate pipe "http://www.technearts.net/pipe/Pipe"

Model:
	elements+=AbstractElement*;

AbstractElement:
	Variable | EvalExpression;

EvalExpression:
	expression=Expression;

Variable:
	name=ID ATTR expression=Expression;

Expression:
	Piping;

Piping returns Expression:
	Or ({Pipe.left=current} PIPE right=Or)*;

Or returns Expression:
	And ({Or.left=current} _OR_ right=And)*;

And returns Expression:
	Equality ({And.left=current} _AND_ right=Equality)*;

Equality returns Expression:
	Comparison ({Equality.left=current} op=(_EQ_ | _NE_) right=Comparison)*;

Comparison returns Expression:
	PlusOrMinus ({Comparison.left=current} op=(_GT_ | _LT_ | _GE_ | _LE_) right=PlusOrMinus)*;

PlusOrMinus returns Expression:
	MulOrDiv (({Plus.left=current} '+' | {Minus.left=current} '-') right=MulOrDiv)*;

MulOrDiv returns Expression:
	Primary (({MulOrDiv.left=current} op=('*' | '/')) right=Primary)*;

Primary returns Expression:
	'(' Expression ')' | 
	{Not} "!" expression=Atomic | 
	Atomic | 
	{Array} '[' values+=Expression* ']' | 
	{Map} '{' (key+=Key ATTR value+=Expression)* '}';

Atomic returns Expression:
	NumberConstant | StringConstant | BoolConstant | VariableRef | PatternMatch;

PatternMatch:
	pattern=('?:' | ':?' | '?:?' | '?');

VariableRef:
	variable=[Variable];

NumberConstant:
	value=NUMBER;

StringConstant:
	value=STRING;

BoolConstant:
	value=BOOLEAN;
	
Key: 
	ID | STRING | NUMBER | BOOLEAN
;

terminal ATTR:
	':';

terminal PIPE:
	'>>';

terminal _OR_:
	'||';

terminal _AND_:
	'&&';

terminal _EQ_:
	'==';

terminal _NE_:
	'!=';

terminal _LE_:
	'<=';

terminal _LT_:
	'<';

terminal _GE_:
	'>=';

terminal _GT_:
	'>';

terminal BOOLEAN:
	'true' | 'false'
;

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal NUMBER returns ecore::EBigDecimal:
	('0'..'9')+ ('.' ('0'..'9')+)?;

terminal STRING:
	'"' ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | '"'))* '"' |
	"'" ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | "'"))* "'";
	
terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

terminal ANY_OTHER:
	.;
